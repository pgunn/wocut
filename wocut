#!/usr/bin/perl -w

use strict;
use warnings FATAL => qw(all);
use YAML::XS qw/Dump Load DumpFile LoadFile/; # In Debian, you want the libyaml-libyaml-perl package for this.
$YAML::XS::QuoteNumericStrings=0;

my $nevermark = 99999; # Mark that we use to discard packets
my $servicedir = qq{/etc/wocut};

my $debuglvl = 0;
my $doit = 1;
main();

###########################
# wocut - Manage random redirection of incoming connections to one port
#	to a bunch of ports. Manage several such profiles at the same time,
#	treating them like SysVInit services and keeping track of ones to
#	restore on boot, let users start/stop them individually.
#
# Written by Pat Gunn, November 2013. Public Domain.
# Thanks to the #iptables channel for informing me of the feature
# this depends on.

sub main
{
my %cfg = handle_args(@ARGV);
if(	$cfg{cmd} eq 'start')		{dispatch_start(	%cfg);}
elsif(	$cfg{cmd} eq 'stop')		{dispatch_stop(		%cfg);}
elsif(	$cfg{cmd} eq 'define1')		{dispatch_define1(	%cfg);}
elsif(	$cfg{cmd} eq 'undefine')	{dispatch_undefine(	%cfg);}
elsif(	$cfg{cmd} eq 'list')		{dispatch_list(		%cfg);}
elsif(	$cfg{cmd} eq 'stats')		{dispatch_stats(	%cfg);}
}

sub handle_args
{
my @args = @_;
my %cfg;

if(! @args) {usage();}

$cfg{cmd} = shift(@args);
if($cfg{cmd} eq 'list')
	{ # Takes no additional args
	if(@args) {usage();}
	}
elsif(	($cfg{cmd} eq 'start')
||	($cfg{cmd} eq 'stop' )
||	($cfg{cmd} eq 'undefine')
||	($cfg{cmd} eq 'stats') )
	{ # Take one arg, the name of the service
	if(@args != 1) {usage();}
	$cfg{service} = shift(@args);
	}
elsif($cfg{cmd} eq 'define1')
	{ # Take service name and a definition (many components, defer parsing)
	if(@args < 2) {usage();}
	$cfg{service} = shift(@args);
	$cfg{definition} = \@args; # Stash the whole thing for more parsing later
	}
else	{usage();}
return %cfg;
}

sub usage
{
die "Usage: wocut COMMAND\n"
.	"\tCOMMAND is one of:\n"
.	"\t\tlist\n"
.	"\t\tstats SERVICE\n"
.	"\t\tstart SERVICE\n"
.	"\t\tstop SERVICE\n"
.	"\t\tdefine1 SERVICE DEFINITION\n"
.	"\t\tundefine SERVICE\n\n"
.	"A service might be defined as follows:\n"
.	"\twocut define1 apache [auto] [nodirect] 80/tcp to 4113 4114 4115\n";
}

################
# Dispatch functions

sub dispatch_list
{	# List with details. Do we want a shortlist command too?
	# XXX Is it best to have a data-neutral display like this,
	#	or would it be better to make a custom display with
	#	ordered keys?
my %cfg = @_;

my %services = read_services();
foreach my $service (sort {$a cmp $b} keys %services)
	{
	print "Service: $service\n";
	print join("\n", map	{
				"\t$_\t => "
.				(ref($services{$service}{$_}) eq 'ARRAY' ?
				join(", ", @{$services{$service}{$_}})
:				$services{$service}{$_});
				} keys %{$services{$service}});
	print "\n\n";
	}
}

#TODO: Is it worth doing this without the RAW rules we need to get more than initial-packet info?
sub dispatch_stats
{	# "service" key says which one
	# FIXME: This really is shoddy statistics
my %cfg = @_;
my %iconn_data = service_statistics($cfg{service});
print "Total connections initiated: " . $iconn_data{totalpackets} . "\n";
print "\t(Following table does not include the first target port)\n";
foreach my $port (sort {$a <=> $b} keys %{$iconn_data{details}})
	{
	print "\tSent to port $port: " . $iconn_data{details}{$port} . "\n";
	}
}

sub dispatch_start
{ # "service" key says which one
my %cfg = @_;
my $status = service_status($cfg{service});
if($status == 0)
	{die "Service not found\n";}
elsif($status == 2)
	{die "Service already started\n";}
elsif($status == 1)
	{
	do_nevermark(); # Always install this.
	my %svcdata = read_services();
	ipt_establish($cfg{service}, %{$svcdata{$cfg{service}}});
	print "Started\n";
	}
else
	{die "Internal error: Unknown status for service\n";}
}

sub dispatch_stop
{ # "service" key says which one
my %cfg = @_;
my $status = service_status($cfg{service});
if($status == 0)
	{die "Service not found\n";}
elsif($status == 1)
	{die "Service not running\n";}
elsif($status == 2)
	{
	my %svcdata = read_services();
	ipt_remove($cfg{service}, $svcdata{$cfg{service}});
	print "Stopped\n";
	}
else
	{die "Internal error: Unknown status for service\n";}
}

sub dispatch_define1
{ # "service" key says which one, "definition" key is a listref that defines the service.
# What kind of syntax is best?
#	wocut define1 apache [auto] [nodirect] 80/tcp to 4113 4114 4115
# or
#	wocut define apache auto=on allowdirect=false port=80 proto=tcp dport=4113,4114,4115
# or
#	wocut define apache:auto:nodirect:port=80:proto=tcp:dport=4113,4114,4115
# or....
# Let's just go with the first for now. Tired of indecision. In fact, we'll call this "define1"
#	and maybe allow other syntaces with "defineX". Take that, second guessing myself!
#
# Our goal in this function is to parse the CLI into the same structures used for read_services()
# and then to call save_service.

my %cfg = @_;
my %service; # Used to hold the definition we're building

$service{auto} = 0;
$service{blockdirect} = 0;

if(${$cfg{definition}}[0] eq "auto")
	{
	shift(@{$cfg{definition}}); # Remove it for easy parsing
	$service{auto} = 1;
	}
if(${$cfg{definition}}[0] eq "nodirect")
	{
	shift(@{$cfg{definition}}); # Remove it for easy parsing
	$service{blockdirect} = 1;
	}
my ($indef, $to, @ports) = @{$cfg{definition}};
if($to ne 'to') {die "Invalid syntax for service definition\n";} # Sanity check
$indef =~ /^(\d+)\/(\w+)$/;
my ($inport, $proto) = ($1, $2);
if(	($proto eq 'tcp')
||	($proto eq 'udp'))
	{$service{proto} = $proto;}
else	{die "Invalid syntax for service definition\n";}
# XXX Option: Check port range for inclusion, for both in and out.
$service{inport} = $inport;
$service{markports} = \@ports;

save_service($cfg{service}, %service);
}

sub dispatch_undefine
{ # "service" key defines which one. Implies "stop".
my %cfg = @_;

my $status = service_status($cfg{service});
if($status == 2)
	{dispatch_stop(%cfg);} # FIXME Add additional layer so dispatch functions don't call dispatch functions

unlink("$servicedir/$cfg{service}.wocut");
}

#################
# Handle wocut files

sub read_services
{	# Read all services from the servicedir into a hash, return that. Keys are shortnames
	# The value of that hash is another hash with keys:
	#	inport
	#	ports
	#	description
	#	proto (tcp or udp usually, likely won't work with anything else)
	#	auto
	#	blockdirect
	# TODO: If we want to get fancy, report on potential port conflicts.
opendir(WODIR, $servicedir) || die "Failed to open wocut servicedir [$servicedir]: $!\n";
my @servicefiles = 	grep	{/\.wocut$/}
			readdir(WODIR);
dprint(2,"D: Parsing " . join(',', @servicefiles) . "\n");
close(WODIR);
my %services;
map	{
	my ($svc, %cfg) = _read_service("$servicedir/$_");
	$services{$svc} = \%cfg;
	} @servicefiles;
return %services;
}

sub save_service
{	# Save a service the user has provided via CLI into the servicedir
my ($svcname, %cfg) = @_;

_write_service("$servicedir/$svcname.wocut", %cfg);
}

sub serviceauto_set
{
my ($svc, $truefalse) = @_;
my %data = _read_service("$servicedir/$svc.wocut");
$data{auto} = $truefalse ? 1 : 0;
_write_service("$servicedir/$svc.wocut", %data);
}

sub serviceautos_read
{
my ($svc) = @_;
my %data = _read_service("$servicedir/$svc.wocut");
return $data{auto};
}

sub service_status
{ # Report on if service is defined, and if it is, whether it's enabled.
	# This doesn't guarantee the service is working; it only checks
	# One rule in the chain. If users go in and add/delete/change rules, they're on their own.
	# Returns:
	#	0 - If the service is not defined
	#	1 - If the service is defined but not active
	#	2 - If the service is defined and active

my ($svcname) = @_;

my %allsvc = read_services();
if(defined $allsvc{$svcname})
	{
	if(grep{/wocut-$svcname:/} qx/iptables -t nat -v -n -L PREROUTING/)
		{return 2;}
	else	{return 1;}
	}
return 0; # Service is not defined.
}

sub service_statistics
{ # Returns usage statistics on the named service based on iptables counters.
	# TODO: Offer the option of inserting no-target rules in the raw table to actually count
	#	traffic. When we do that, we can look for those here.
my ($svcname) = @_;

my @lines = grep {/wocut-$svcname:/}
	map {s/^\s+//; $_} # Remove the pre-padding
	grep {! /^Chain/}
	qx/iptables -t mangle -v -n -x -L PREROUTING/; # The -x flag gives us raw numbers rather than suffices.

# So now @lines will be of format:
# $packets $bytes $RULE
my ($remarkline) = grep {/wocut-$svcname: initialmark/} @lines;
my @redirlines = grep {/wocut-$svcname: remark/} @lines;

# Looking at the packets listing will give us an estimate for the number of connections routed
# through this mechanism. The bytes metric here is useless, as it only shows bytes from initial packets.

my ($ipackets) = split(/\s+/, $remarkline);
my %sstats;
foreach my $redirline (@redirlines)
	{
	my ($rpackets) = split(/\s+/, $redirline);
	$redirline =~ /remark for port (\d+)/;
	my $rport = $1;
	$sstats{$rport} = $rpackets;
	}
return (	totalpackets => $ipackets,
		details => \%sstats);
}

sub _read_service
{ # Read a particular servicefile. 
my ($infile) = @_;

($infile =~ /.*\/([^.]+)\.wocut$/) || die "Internal error: Can't parse servicename for [$infile]\n";
my $servicename = $1;
my $svcdata = LoadFile($infile) || die "Failed to read wofile [$infile]\n";
return ($servicename, %$svcdata);
}

sub _write_service
{ # Save YAML to a service file.
my ($file, %data) = @_;

DumpFile($file, \%data) || die "Failed to write wofile [$file]\n";
}

###############
# Handle iptables rules

sub ipt_is_present
{	# Note that this does *NOT* check correctness by the current config; it just looks for a single correct crumb. If the user's fucked with the rules,
	# that won't be caught. Be careful.
my ($svcname) = @_;
my @rules_mangle	= `iptables -v -n --line-numbers -L PREROUTING -t mangle`;
map	{
	my ($ruleno) = split(/\s+/);
	return 1;
	} grep{/wocut-$svcname:/} @rules_mangle;
return 0;
}

sub ipt_establish
{
my ($svcname, %service) = @_;
my @dports = @{$service{markports}};
_ipt_do("mangle", "PREROUTING", "-p $service{proto} -m state --state NEW --dport $service{inport} -j MARK --set-mark $dports[0]", "wocut-$svcname: initialmark");
if($service{blockdirect})
	{
	map {_ipt_do("mangle", "PREROUTING", "-p $service{proto} --dport $_ -j MARK --set-mark $nevermark", "wocut-$svcname: blockdirect");} @dports;
	}

for(my $portiter = 1, my $probiter = @dports; $portiter < @dports; $portiter++,$probiter--)
	{
	my $lastmark = $dports[$portiter - 1];
	my $probslice = 1 - (1/$probiter);
	_ipt_do("mangle", "PREROUTING", "-p $service{proto} -m mark --mark $lastmark -m statistic --mode random --probability $probslice -j MARK --set-mark $dports[$portiter]", "wocut-$svcname: remark for port $dports[$portiter]");
	}
foreach my $dport (@dports)
	{
	_ipt_do("nat", "PREROUTING", "-p $service{proto} -m mark --mark $dport -j REDIRECT --to-port $dport", "wocut-$svcname: redirect to port $dport");
	}
}

sub ipt_remove
{ # Cleanly remove a wocut ruleset from iptables
my ($svcname) = @_;

my @rules_mangle	= `iptables -v -n --line-numbers -L PREROUTING -t mangle`;
my @rules_nat		= `iptables -v -n --line-numbers -L PREROUTING -t nat`;
dprint(2, "ipt_remove asked to remove service $svcname\n");
# There problem with doing this forward-iteratively with a map is that each deletion changes future indices.
# So instead, we find the rules that match our service, extract the line numbers from them,
# reverse-sort them, and then remove them last-first, so all the indices remain valid until
# they're removed. 

map	{
	system("iptables -t mangle -D PREROUTING $_");
	} sort {$b <=> $a} map {(split(/\s+/))[0]} grep{/wocut-$svcname:/} @rules_mangle;
map	{
	system("iptables -t nat -D PREROUTING $_");
	}  sort {$b <=> $a} map {(split(/\s+/))[0]} grep{/wocut-$svcname:/} @rules_nat;
}

sub _ipt_do
{
my ($table, $chain, $contents, $comment) = @_;
docmd("iptables -t $table -A $chain $contents -m comment --comment \"$comment\"");
}

sub do_nevermark
{ # This establishes rules for packet dropping based on marks in the three chains that are the last to see a packet
foreach my $fchain (qw/INPUT OUTPUT FORWARD/)
	{
	if(! ipt_rule_present("wocut: nevermark", "filter", $fchain))
		{
		_ipt_do("filter", $fchain, "-m mark --mark $nevermark -j DROP", "wocut: nevermark");
		}
	}
}

sub ipt_rule_present
{ # Is a rule with the given ruleid (comment) present in the named table/chain?
my ($ruleid, $table, $chain) = @_;

my @rules = `iptables -v -n -L $chain -t $table`;
return scalar grep(/$ruleid/,@rules);
}

#################
# My normal utilities


sub docmd($)
{
my ($cmdline) = @_;
dprint(2, "IPTABLES: [$cmdline]");
if($doit)
	{
	qx/$cmdline/;
	if($? != 0)
		{die "************Command failed!**********\n";}
	}
}

sub dprint($$)
{
my ($loglvl, $msg) = @_;
if($debuglvl >= $loglvl)
	{print "$msg\n";}
}

###################
# It'd be nice to have a set of utility functions that would just parse iptables output, to reduce code duplication.
# Pity the output is so unstructured.
# If we really want to do this right, we need to either:
#	1) always pass the same arguments to iptables to get this information
#		and not cut bits out before handing them to these functions, or
# 	2) Develop and stick to a terminology for the structures we expect are in the string
#
#	To be clear, we're talking about things like -v, -n, and --line-numbers

# sub ipt_hash
# ...

